/**
 * Inject these into all the subprojects
 */
subprojects {
	apply plugin: 'java'

	repositories {
		mavenCentral()
	}

	dependencies {
		// the production code uses log4j logging api at compile time
		compile 'log4j:log4j:1.2.17'

		// javassit byte-code engineering library used to divert diffused methods at class load time
		compile 'javassist:javassist:3.12.1.GA'

		// command line options parsing library
		compile 'net.sf.jopt-simple:jopt-simple:4.5'

		// apache commons
		compile 'org.apache.commons:commons-io:1.3.2'

		// freezedry persistence JAR
		compile files( '../ext-libs/FreezeDry_0.2.6.jar' )

		// for testing
		testCompile 'junit:junit:4.11'
	}

	project.ext {
		sourceCompatibility = 1.7
		version = '0.2.2'
		app_name = 'Diffusive'
	}

	// sets the version number based on whether building the release or snapshot
	gradle.taskGraph.whenReady { taskGraph ->
		if( taskGraph.hasTask( release ) ) {
			version = project.ext.version
		} else {
			version = project.ext.version + '-SNAPSHOT'
		}
	}

	jar {
		manifest {
			attributes(
					"Implementation-Vendor": "Rob Philipp",
					"Implementation-Title": project.ext.app_name,
					"Implementation-Version": project.ext.version
			)
		}
	}

	task release {

	}

}

